allprojects {
    group = 'cn.jia'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    dependencies {
        implementation libs.slf4j.api
        implementation libs.jakarta.inject.api
        implementation libs.jakarta.annotation.api
        implementation libs.mapstruct.core
        compileOnly 'com.intellij:annotations:+@jar'
        annotationProcessor libs.lombok
        annotationProcessor libs.mapstruct.processor
        compileOnly libs.lombok
        testAnnotationProcessor libs.lombok
        testCompileOnly libs.lombok
    }

    sourceCompatibility = JavaVersion.VERSION_17

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
//        options.compilerArgs << '-Xlint:deprecation'
    }

    sourceSets {
        main {
            resources {
                srcDirs = ['src/main/resources']
            }
        }
    }

    test {
        useJUnitPlatform()
    }

//    processTestResources.dependsOn copyTestResources

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? rootProject.ext.snapshotsRepoUrl : rootProject.ext.releasesRepoUrl
                    credentials {
                        username rootProject.ext.repoUsername
                        password rootProject.ext.repoPassword
                    }
                }
            }
        }
    }
}

tasks.register('publishAllToLocal') {
    dependsOn subprojects*.publishToMavenLocal
}

tasks.register('publishAllToMavenRepository') {
    dependsOn subprojects*.publishAllPublicationsToMavenRepository
}